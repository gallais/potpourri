\begin{code}
module Serialised.Desc

import Data.Buffer
import Data.Fin
import Data.String
import Data.Vect

import Decidable.Equality

import Lib

%default total

------------------------------------------------------------------------
-- Types
------------------------------------------------------------------------

||| A description is a nested tuple of Bytes or recursive positions
||| It is indexed by 1 'input' parameter and 2 'ouput' indices:
|||  @rightmost telling us whether we are in the rightmost subterm
|||             in which case `Rec` won't need to record an additional offset
|||  @size      the statically known part of the size (in number of bytes)
|||  @offsets   the dynamically known part of the size (in number of subtrees)
public export
\end{code}
%<*desctype>
\begin{code}
data Desc : (rightmost : Bool) ->
            (size : Nat) -> (offsets : Nat) ->
            Type
\end{code}
%</desctype>
%<*desc>
\begin{code}
data Desc where
  None : Desc r 0 0
  Byte : Desc r 1 0
  Prod : {sl, sr, ol, or : Nat} ->
         Desc False sl ol -> Desc r sr or ->
         Desc r (sl + sr) (ol + or)
  Rec : Desc r 0 (ifThenElse r 0 1)
\end{code}
%</desc>
\begin{code}
||| A constructor description is essentially an existential type
||| around a description
||| @ nm is the type of name we use for the constructor
public export
\end{code}
%<*constructor>
\begin{code}
record Constructor (nm : Type) where
  constructor (::)
  name : nm
  {size : Nat}
  {offsets : Nat}
  description : Desc True size offsets
\end{code}
%</constructor>
\begin{code}

||| A data description is a sum over a list of constructor types
public export
\end{code}
%<*data>
\begin{code}
record Data (nm : Type) where
  constructor MkData
  {consNumber : Nat}
  constructors : Vect consNumber (Constructor nm)
\end{code}
%</data>
\begin{code}

||| A wrapper around fin that helps unification
public export
\end{code}
%<*index>
\begin{code}
record Index (cs : Data nm) where
  constructor MkIndex
  getIndex : Fin (consNumber cs)
\end{code}
%</index>
\begin{code}

||| A smart projection
public export
\end{code}
%<*description>
\begin{code}
description : {cs : Data nm} -> (k : Index cs) ->
              let cons = index (getIndex k) (constructors cs) in
              Desc True (size cons) (offsets cons)
description {cs} k
  = description (index (getIndex k) (constructors cs))
\end{code}
%</description>
\begin{code}

||| A little bit of magic
public export
isConstructor : Eq nm => nm -> (cs : Data nm) -> Maybe (Fin cs.consNumber)
isConstructor x cs = findIndex ((x ==) . Constructor.name) (constructors cs)

public export
\end{code}
%<*fromString>
\begin{code}
fromString : {cs : Data String} -> (str : String) ->
             {auto 0 _ : IsJust (isConstructor str cs)} ->
             Index cs
fromString {cs} str with (isConstructor str cs)
 _ | Just k = MkIndex k
\end{code}
%</fromString>
\begin{code}

||| Another kind of magic
public export
fromInteger : {cs : Data {nm}} -> (k : Integer) ->
              {auto 0 _ : So (consNumber cs /= 0)} ->
              Index cs
fromInteger {cs = MkData (_::_)} k = MkIndex (restrict _ k)

export
DecEq (Index cs) where
  decEq (MkIndex k) (MkIndex l) with (decEq k l)
    decEq (MkIndex k) (MkIndex .(k)) | Yes Refl = Yes Refl
    decEq (MkIndex k) (MkIndex l) | No notEq = No (notEq . cong getIndex)

------------------------------------------------------------------------
-- Show instances

export
Show (Desc r s n) where
  showPrec _ None = "()"
  showPrec _ Byte = "Bits8"
  showPrec p (Prod d e) =
    showParens (Open < p) $ "Tuple \{showPrec App d} \{showPrec App e}"
  showPrec _ Rec = "X"

export
Show (Data {nm}) where
  show cs = go (constructors cs) where

    go : Vect n (Constructor _) -> String
    go [] = "⊥"
    go (c :: cs) = concat
      $  ("μX. " ++ show (description c))
      :: ((" + " ++) <$> map (\ c => show (description c)) cs)

------------------------------------------------------------------------
-- Eq instances

||| Heterogeneous equality check for descriptions
eqDesc : Desc r s n -> Desc r' s' n' -> Bool
eqDesc None None = True
eqDesc Byte Byte = True
eqDesc (Prod d e) (Prod s t) = eqDesc d s && eqDesc e t
eqDesc Rec Rec = True
eqDesc _ _ = False

export
eqConstructor : Constructor a -> Constructor b -> Bool
eqConstructor (_ :: d) (_ :: e) = eqDesc d e

||| Heterogeneous equality check for vectors of constructors
eqConstructors : Vect m (Constructor a) -> Vect n (Constructor b) -> Bool
eqConstructors [] [] = True
eqConstructors (c :: cs) (c' :: cs') = eqConstructor c c' && eqConstructors cs cs'
eqConstructors _ _ = False

export
eqData : Data a -> Data b -> Bool
eqData (MkData cs) (MkData cs') = eqConstructors cs cs'

------------------------------------------------------------------------
-- Serialisation of descriptions to a Buffer

parameters (buf : Buffer)

  ||| Set a description in a buffer
  ||| @ start position in the buffer to set the description at
  ||| @ d     description to serialise
  ||| Returns the end position
  setDesc : (start : Int) -> (d : Desc r s n) -> IO Int
  setDesc start None = (start + 1) <$ setBits8 buf start 0
  setDesc start Byte = (start + 1) <$ setBits8 buf start 1
  setDesc start (Prod d e)
    = do setBits8 buf start 2
         afterLeft <- setDesc (start + 1) d
         setDesc afterLeft e
  setDesc start Rec = (start + 1) <$ setBits8 buf start 3

  ||| Set a list of constructors one after the other in a buffer
  ||| @ start position in the buffer to set the constructors at
  ||| @ cs    list of constructors to serialise
  ||| Returns the end position
  setConstructors : (start : Int) -> (cs : Vect n (Constructor _)) -> IO Int
  setConstructors start [] = pure start
  setConstructors start ((_ :: d) :: cs)
    = do afterC <- setDesc start d
         setConstructors afterC cs

  ||| Set data description in a buffer
  ||| @ start position in the buffer to set the data description at
  ||| @ cs    data description to serialise
  ||| Returns the end position
  export
  setData : (start : Int) -> (cs : Data nm) -> IO Int
  setData start (MkData {consNumber} cs) = do
    -- We first store the length of the list so that we know how
    -- many constructors to deserialise on the way out
    setBits8 buf start (cast consNumber)
    setConstructors (start + 1) cs

  ||| Existential wrapper to deserialise descriptions
  ||| @ rightmost is known statically but the others indices are not
  record IDesc (rightmost : Bool) where
    constructor MkIDesc
    {size : Nat}
    {offsets : Nat}
    runIDesc : Desc rightmost size offsets

  ||| Auxiliary definition to help idris figure out the types of
  ||| everything involved
  IProd : IDesc False -> IDesc r -> IDesc r
  IProd (MkIDesc d) (MkIDesc e) = MkIDesc (Prod d e)

  ||| Get a description from a buffer
  ||| @ start position the description starts at in the buffer
  ||| Returns the end position & the description
  getDesc : {r : Bool} -> Int -> IO (Int, IDesc r)
  getDesc start = case !(getBits8 buf start) of
    0 => pure (start + 1, MkIDesc None)
    1 => pure (start + 1, MkIDesc Byte)
    2 => do (afterLeft, d) <- assert_total (getDesc {r = False} (start + 1))
            (end, e) <- assert_total (getDesc {r} afterLeft)
            pure (end, IProd d e)
    3 => pure (start + 1, MkIDesc Rec)
    _ => failWith "Invalid Description"

  ||| Get a list of constructor from a buffer
  ||| @ start position the list of constructors starts at in the buffer
  ||| @ n     number of constructors to deserialise
  getConstructors : (start : Int) -> (n : Nat) -> IO (Vect n (Constructor ()))
  getConstructors start 0 = pure []
  getConstructors start (S n)
    = do (afterD, d) <- getDesc start
         cs <- getConstructors afterD n
         pure ((() :: runIDesc d) :: cs)

  ||| Get a data description from a buffer
  ||| @ start position the data description starts at in the buffer
  export
  getData : Int -> IO (Data {nm = ()})
  getData start = do
    n <- getBits8 buf start
    let Just n = ifThenElse (n < 0) Nothing (Just (cast n))
       | Nothing => failWith "Invalid header"
    MkData <$> getConstructors (start + 1) n


------------------------------------------------------------------------
-- Meaning of descriptions as functors

namespace Data

  ||| Meaning where subterms are interpreted using the parameter
  public export
\end{code}
%<*meaning>
\begin{code}
  Meaning : Desc r s n -> Type -> Type
  Meaning None x = ()
  Meaning Byte x = Bits8
  Meaning (Prod d e) x = Tuple (Meaning d x) (Meaning e x)
  Meaning Rec x = x
\end{code}
%</meaning>
\begin{code}

  public export
\end{code}
%<*fmap>
\begin{code}
  fmap : (d : Desc r s o) -> (a -> b) -> Meaning d a -> Meaning d b
  fmap None f v = v
  fmap Byte f v = v
  fmap (Prod d e) f (v # w) = (fmap d f v # fmap e f w)
  fmap Rec f v = f v
\end{code}
%</fmap>
\begin{code}

  export
  fmapId : (d : Desc r s o) -> (f : a -> a) -> ((x : a) -> f x === x) ->
           (t : Meaning d a) -> fmap d f t === t
  fmapId None f eq t = Refl
  fmapId Byte f eq t = Refl
  fmapId (Prod d e) f eq (t # u) = cong2 (#) (fmapId d f eq t) (fmapId e f eq u)
  fmapId Rec f eq t = eq t

  public export
\end{code}
%<*traverse>
\begin{code}
  traverse : Monad m => (d : Desc r s o) ->
             (a -> m b) -> Meaning d a -> m (Meaning d b)
  traverse None f v = pure v
  traverse Byte f v = pure v
  traverse (Prod d e) f (v # w) = [| traverse d f v # traverse e f w |]
  traverse Rec f v = f v
\end{code}
%</traverse>
\begin{code}

------------------------------------------------------------------------
-- Meaning of data descriptions as fixpoints

  public export
\end{code}
%<*alg>
\begin{code}
  Alg : Data nm -> Type -> Type
  Alg cs x = (k : Index cs) -> Meaning (description k) x -> x
\end{code}
%</alg>
\begin{code}

  ||| Fixpoint of the description:
  ||| 1. pick a constructor
  ||| 2. give its meaning where subterms are entire subtrees
  public export
\end{code}
%<*mu>
\begin{code}
  data Mu : Data nm -> Type where
    (#) : Alg cs (assert_total (Mu cs))
\end{code}
%</mu>
\begin{code}

  export
\end{code}
%<*unfoldAs>
\begin{code}
  %unsafe
  0 unfoldAs :
    (k : Index cs) -> (0 t : Data.Mu cs) ->
    (val : Data.Meaning (description k) (Data.Mu cs)
    ** t === (k # val))
\end{code}
%</unfoldAs>
\begin{code}

  ||| Fixpoints are initial algebras
  public export
\end{code}
%<*fold>
\begin{code}
  fold : {cs : Data nm} -> Alg cs a -> Mu cs -> a
  fold alg (k # t) = alg k (assert_total $ fmap _ (fold alg) t)
\end{code}
%</fold>
\begin{code}

------------------------------------------------------------------------
-- Examples

namespace Tree

  public export
\end{code}
%<*treeleaf>
\begin{code}
  Leaf : Constructor String
  Leaf = "Leaf" :: None
\end{code}
%</treeleaf>
\begin{code}

  public export
\end{code}
%<*treenode>
\begin{code}
  Node : Constructor String
  Node = "Node" :: Prod Rec (Prod Byte Rec)
\end{code}
%</treenode>
\begin{code}

  public export
\end{code}
%<*treedesc>
\begin{code}
  Tree : Data String
  Tree = MkData [Leaf, Node]
\end{code}
%</treedesc>
\begin{code}

\end{code}
%<*indexleaf>
\begin{code}
  indexLeaf : Index Tree
  indexLeaf = "Leaf"
\end{code}
%</indexleaf>

\end{code}
%<*notindexcons>
\begin{code}
  failing
    notIndexCons : Index Tree
    notIndexCons = "Cons"
\end{code}
%</notindexcons>
\begin{code}
  public export
\end{code}
%<*leaf>
\begin{code}
  leaf : Mu Tree
  leaf = "Leaf" # ()
\end{code}
%</leaf>
\begin{code}

  public export
\end{code}
%<*node>
\begin{code}
  node : Mu Tree -> Bits8 -> Mu Tree -> Mu Tree
  node l b r = "Node" # l # b # r
\end{code}
%</node>
\begin{code}

  public export
\end{code}
%<*example>
\begin{code}
  example : Mu Tree
  example = node (node (node leaf 1 leaf) 5 leaf)
                 10
                 (node leaf 20 leaf)
\end{code}
%</example>
