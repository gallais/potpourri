\begin{code}
module SaferIndexed

import Data.Fin
import Data.DPair
import Data.Vect
import Data.Buffer
import System.File.Buffer

import Decidable.Equality

import Data.String
import Data.Singleton

import Lib
import Serialised.Desc

%default total

blockSize : Int
blockSize = 1000

namespace Data

  data All' : (d : Desc r s n) -> (p : x -> Type) -> Meaning d x -> Type

  public export

\end{code}
%<*allquant>
\begin{code}
  All : (d : Desc r s n) -> (p : x -> Type) -> Meaning d x -> Type
  All None p t = ()
  All Byte p t = Singleton t
  All d@(Prod _ _) p t = All' d p t
  All Rec p t = p t
\end{code}
%</allquant>
\begin{code}

  public export

\end{code}
%<*dataallquant>
\begin{code}
  data All' : (d : Desc r s n) -> (p : x -> Type) -> Meaning d x -> Type where
    (#) : All d p t -> All e p u -> All' (Prod d e) p (t # u)
\end{code}
%</dataallquant>
\begin{code}

  export
  all : (d : Desc r s n) -> ((a : x) -> p a) ->
        (t : Meaning d x) -> All d p t
  all None f t = t
  all Byte f t = MkSingleton t
  all (Prod d e) f (t # u) = (all d f t # all e f u)
  all Rec f t = f t

  public export
  AllK : (d : Desc r s n) -> (p : x -> Type) -> Meaning d x -> Type -> Type
  AllK None p t r = r
  AllK Byte p t r = Singleton t -> r
  AllK (Prod d e) p t r = AllK d p (fst t) (AllK e p (snd t) r)
  AllK Rec p t r = p t -> r

  export
  curry : {0 p : x -> Type} -> (d : Desc r s n) ->
          {0 t : Meaning d x} -> (All d p t -> a) -> AllK d p t a
  curry None f = f ()
  curry Byte f = f
  curry (Prod d e) f
    = SaferIndexed.Data.curry d {a = AllK e p (snd t) a}
    $ \ v => SaferIndexed.Data.curry e
    $ \ w => f (rewrite etaTuple t in v # w)
  curry Rec f = f

namespace Serialising

  export
  record Serialising (cs : Data nm) (t : Data.Mu cs) where
    constructor MkSerialising
    runSerialising : Buffer -> Int -> IO Int

  export
  (>>=) : IO a -> (a -> Serialising cs t) -> Serialising cs t
  io >>= f = MkSerialising $ \buf, start =>
               do x <- io
                  runSerialising (f x) buf start

  parameters (buf : Buffer)

    goMeaning : Int ->
                {r : Bool} -> (d : Desc r s n) ->
                {0 t : Meaning d (Data.Mu cs)} ->
                All d (Serialising cs) t ->
                IO (Vect n Int, Int)
    goMeaning start None layer = pure ([], start)
    goMeaning start Byte layer = ([], start + 1) <$ setBits8 buf start (getSingleton layer)
    goMeaning start (Prod d e) (v # w)
      = do (n1, afterLeft) <- goMeaning start d v
           (n2, afterRight) <- goMeaning afterLeft e w
           pure (n1 ++ n2, afterRight)
    goMeaning start Rec layer
      = do end <- runSerialising layer buf start
           pure $ (,end) $ if r then [] else [end - start]

    goMu : Int ->
           (k : Fin (consNumber cs)) -> (c : Constructor _) ->
           {0 t : Meaning (description c) (Data.Mu cs)} ->
           All (description c) (Serialising cs) t ->
           IO Int
    goMu start k cons layer
      = do -- [ Tag | ... offsets ... | ... meaning ... ]
           setBits8 buf start (cast $ cast {to = Nat} k)
           let afterTag  = start + 1
           let afterOffs = afterTag + 8 * cast (offsets cons)
           (is, end) <- goMeaning afterOffs (description cons) layer
           setInts buf afterTag is
           pure end

  export
\end{code}
%<*serialisemu>
\begin{code}
  (#) : {cs : Data nm} -> (k : Index cs) ->
        {0 t : Meaning (description k) (Data.Mu cs)} ->
        All (description k) (Serialising cs) t ->
        Serialising cs (k # t)
\end{code}
%</serialisemu>
\begin{code}
  MkIndex k # layer
    = MkSerialising $ \ buf, start =>
        goMu buf start k (index k $ constructors cs) layer

  export
  setMuK : (cs : Data nm) -> (k : Index cs) ->
           {0 t : Meaning (description k) (Data.Mu cs)} ->
           AllK (description k) (Serialising cs) t (Serialising cs (k # t))
  setMuK cs (MkIndex k) with (index k $ constructors cs)
    _ | cons = SaferIndexed.Data.curry (description cons) $ \ layer =>
               MkSerialising $ \ buf, start => goMu buf start k cons layer

  export
  serialiseToFile : {cs : Data nm} -> String -> {0 t : Mu cs} ->
                    Serialising cs t -> IO ()
  serialiseToFile fp t = do
    Just buf <- newBuffer blockSize
      | Nothing => failWith "\{__LOC__} Couldn't allocate buffer"
    end <- setData buf 8 cs
    setInt buf 0 (end - 8)
    size <- runSerialising t buf end
    Right () <- writeBufferToFile fp buf size
      | Left (err, _) => failWith (show err)
    pure ()

namespace Buffer

  setMu : {cs : Data nm} -> (t : Data.Mu cs) -> Serialising cs t
  setMu (k # t)
    = k # (all (description k) (assert_total Buffer.setMu) t)

  export
  writeToFile : {cs : Data nm} -> String -> Mu cs -> IO ()
  writeToFile fp t = serialiseToFile fp (setMu t)

namespace Pointer

  export
\end{code}
%<*elem>
\begin{code}
  record Meaning (d : Desc r s n) (cs : Data nm)
                 (t : Data.Meaning d (Data.Mu cs)) where
    constructor MkMeaning
    subterms : Vect n Int
    meaningBuffer : Buffer
    meaningPosition : Int
    meaningSize : Int
\end{code}
%</elem>
\begin{code}
  export
\end{code}
%<*pointermu>
\begin{code}
  record Mu (cs : Data nm) (t : Data.Mu cs) where
    constructor MkMu
    muBuffer : Buffer
    muPosition : Int
    muSize : Int
\end{code}
%</pointermu>
\begin{code}

  namespace Poke


    public export
    data Poke' : (d : Desc r s n) -> (cs : Data nm) ->
                 Data.Meaning d (Data.Mu cs) -> Type

    public export
\end{code}
%<*pokedatafun>
\begin{code}
    Poke : (d : Desc r s n) -> (cs : Data nm) ->
           Data.Meaning d (Data.Mu cs) -> Type
    Poke None _ t = ()
    Poke Byte cs t = Singleton t
    Poke d@(Prod _ _) cs t = Poke' d cs t
    Poke Rec cs t = Pointer.Mu cs t
\end{code}
%</pokedatafun>
\begin{code}

\end{code}
%<*pokedatadata>
\begin{code}
    data Poke' : (d : Desc r s n) -> (cs : Data nm) ->
                 Data.Meaning d (Data.Mu cs) -> Type where
      (#) : Pointer.Meaning d cs t ->
            Pointer.Meaning e cs u ->
            Poke' (Prod d e) cs (t # u)
\end{code}
%</pokedatadata>
\begin{code}

    export
\end{code}
%<*pokefun>
\begin{code}
    poke : {0 cs : Data nm} -> {d : Desc r s n} ->
           forall t. Pointer.Meaning d cs t ->
           IO (Poke d cs t)
\end{code}
%</pokefun>
%<*pokefunNone>
\begin{code}
    poke {d = None} el = pure ()
\end{code}
%</pokefunNone>
%<*pokefunByte>
\begin{code}
    poke {d = Byte} el = do
      bs <- getBits8 (meaningBuffer el) (meaningPosition el)
      pure (unsafeMkSingleton bs)
\end{code}
%</pokefunByte>
%<*pokefunProd>
\begin{code}
    poke {d = Prod {sl, ol} d e} {t} (MkMeaning sub buf pos size) = do
      let (subl, subr) = splitAt ol sub
      let sizel = sum subl + cast sl
      let left = MkMeaning subl buf pos sizel
      let posr = pos + sizel
      let right = MkMeaning subr buf posr (size - sizel)
      pure (rewrite etaTuple t in left # right)
\end{code}
%</pokefunProd>
%<*pokefunRec>
\begin{code}
    poke {d = Rec} (MkMeaning _ buf pos size) = pure (MkMu buf pos size)
\end{code}
%</pokefunRec>
%</pokefunbody>
\begin{code}
  namespace Layer
    public export
    data Layer' : (d : Desc r s n) -> (cs : Data nm) -> Data.Meaning d (Data.Mu cs) -> Type

    public export
\end{code}
%<*layerdata>
\begin{code}
    Layer : (d : Desc r s n) -> (cs : Data nm) ->
            Data.Meaning d (Data.Mu cs) -> Type
    Layer None _ _ = ()
    Layer Byte _ t = Singleton t
    Layer d@(Prod _ _) cs t = Layer' d cs t
    Layer Rec cs t = Pointer.Mu cs t

    data Layer' : (d : Desc r s n) -> (cs : Data nm) ->
                  Data.Meaning d (Data.Mu cs) -> Type where
      (#) : Layer d cs t -> Layer e cs u -> Layer' (Prod d e) cs (t # u)
\end{code}
%</layerdata>
\begin{code}

    export
\end{code}
%<*layerfun>
\begin{code}
    layer : {0 cs : Data nm} -> {d : Desc r s n} ->
            forall t. Pointer.Meaning d cs t -> IO (Layer d cs t)
    layer el = poke el >>= go d where

      go : forall r, s, n. (d : Desc r s n) ->
           forall t. Poke d cs t -> IO (Layer d cs t)
      go None p = pure ()
      go Byte p = pure p
      go (Prod d e) (p # q) = [| layer p # layer q |]
      go Rec p = pure p
\end{code}
%</layerfun>
\begin{code}

  public export
\end{code}
%<*outdata>
\begin{code}
  data Out : (cs : Data nm) -> (t : Data.Mu cs) -> Type where
    (#) : (k : Index cs) ->
          forall t. Pointer.Meaning (description k) cs t ->
          Out cs (k # t)
\end{code}
%</outdata>
\begin{code}

  export
\end{code}
%<*outfun>
\begin{code}
  out : {cs : Data nm} -> forall t. Pointer.Mu cs t ->
        IO (Out cs t)
\end{code}
%</outfun>
%<*outfunbody>
\begin{code}
  out {t} mu = do
    tag <- getBits8 (muBuffer mu) (muPosition mu)
    let Just k = MkIndex <$> natToFin (cast tag) (consNumber cs)
      | _ => failWith "Invalid representation"
    let 0 sub = unfoldAs k t
    val <- (k #) <$> getConstructor k {t = sub.fst} (rewrite sym sub.snd in mu)
    pure (rewrite sub.snd in val)
\end{code}
%</outfunbody>
\begin{code}

    where

\end{code}
%<*getOffsets>
\begin{code}
    getOffsets : Buffer -> (pos : Int) ->
      (n : Nat) ->
      forall t. (Vect n Int -> Int -> Pointer.Meaning d cs t) ->
      IO (Pointer.Meaning d cs t)
    getOffsets buf pos 0 k = pure (k [] pos)
    getOffsets buf pos (S n) k = do
      off <- getInt buf pos
      getOffsets buf (8 + pos) n (k . (off ::))
\end{code}
%</getOffsets>
\begin{code}


\end{code}
%<*getConstructor>
\begin{code}
    getConstructor : (k : Index cs) ->
      forall t. Pointer.Mu cs (k # t) ->
      IO (Pointer.Meaning (description k) cs t)
    getConstructor (MkIndex k) mu
      = let offs : Nat; offs = offsets (index k $ constructors cs) in
        getOffsets (muBuffer mu) (1 + muPosition mu) offs
      $ let size = muSize mu - 1 - cast (8 * offs) in
        \ subterms, pos => MkMeaning subterms (muBuffer mu) pos size
\end{code}
%</getConstructor>
\begin{code}

  namespace View

    public export
\end{code}
%<*viewdata>
\begin{code}
    data View : (cs : Data nm) -> (t : Data.Mu cs) -> Type where
      (#) : (k : Index cs) ->
            forall t. Layer (description k) cs t ->
            View cs (k # t)
\end{code}
%</viewdata>
\begin{code}

    export
\end{code}
%<*viewfun>
\begin{code}
    view : {cs : Data nm} ->
           forall t. Pointer.Mu cs t ->
           IO (View cs t)
\end{code}
%</viewfun>
%<*viewfunbody>
\begin{code}
    view ptr = do k # el <- out ptr
                  vs <- layer el
                  pure (k # vs)
\end{code}
%</viewfunbody>
\begin{code}

    export
\end{code}
%<*fmaptype>
\begin{code}
    fmap : (d : Desc{}) ->
           (0 f : Data.Mu cs -> b) ->
           (forall t. Pointer.Mu cs t -> IO (Singleton (f t))) ->
           forall t. Pointer.Meaning d cs t ->
           IO (Singleton (Data.fmap d f t))
\end{code}
%</fmaptype>
%<*fmapfun>
\begin{code}
    fmap d f act ptr = poke ptr >>= go d where

      go : (d : Desc{}) -> forall t. Poke d cs t ->
           IO (Singleton (Data.fmap d f t))
      go None {t} v = pure byIrrelevance
      go Byte v = pure v
      go (Prod d e) (v # w)
        = do fv <- fmap d f act v
             fw <- fmap e f act w
             pure [| fv # fw |]
      go Rec v = act v
\end{code}
%</fmapfun>
\begin{code}

    export
    traverse : Monad m => (d : Desc{}) ->
               (0 f : Mu cs -> m b) ->
               (forall t. Mu cs t -> IO (Singleton (f t))) ->
               forall t. Meaning d cs t ->
               IO (Singleton (Data.traverse d f t))
    traverse d f act v = poke v >>= go d where

      go : (d : Desc{}) -> forall t. Poke d cs t ->
           IO (Singleton (Data.traverse d f t))
      go None {t} v = pure (pure <$> byIrrelevance)
      go Byte v = pure [| pure v |]
      go (Prod d e) (v # w)
        = do v <- traverse d f act v
             w <- traverse e f act w
             let v = [| pure (pure (#)) <*> v |]
             pure [| v <*> w |]
      go Rec v = act v

    export

\end{code}
%<*fold>
\begin{code}
    fold : {cs : Data nm} -> (alg : Alg cs a) ->
           forall t. Mu cs t -> IO (Singleton (Data.fold alg t))
    fold alg ptr
      = do k # t <- out ptr
           rec <- assert_total (fmap _ _ (fold alg) t)
           pure (alg k <$> rec)
\end{code}
%</fold>
\begin{code}

    export
    deserialise : {cs : Data nm} -> forall t. Mu cs t -> IO (Singleton t)
    deserialise ptr = do
      k # ptr <- out ptr
      t <- assert_total (fmap (description k) id deserialise ptr)
      pure ((k #) <$> transport (fmapId (description k) id (\ _ => Refl) _) t)

namespace Serialising

  export
\end{code}
%<*copy>
\begin{code}
  copy : Pointer.Mu cs t -> Serialising cs t
  copy ptr = MkSerialising $ \ buf, pos => do
    let size = muSize ptr
    copyData (muBuffer ptr) (muPosition ptr) size buf pos
    pure (pos + size)
\end{code}
%</copy>
\begin{code}

namespace Tree

  ||| Tree sum
\end{code}
%<*tsum>
\begin{code}
  sum : Data.Mu Tree -> Nat
  sum t = case t of
    "Leaf" # _ => Z
    "Node" # l # b # r =>
      let m = sum l
          n = sum r
      in (m + cast b + n)
\end{code}
%</tsum>
\begin{code}
namespace Raw

  ||| Raw sum
  export
\end{code}
%<*rsum>
\begin{code}
  sum : Pointer.Mu Tree _ -> IO Nat
  sum ptr = case !(view ptr) of
    "Leaf" # _ => pure Z
    "Node" # l # b # r =>
      do m <- sum l
         n <- sum r
         pure (m + cast b + n)
\end{code}
%</rsum>
\begin{code}
namespace Lazy
\end{code}
%<*pokerightmost>
\begin{code}
  rightmost : Pointer.Mu Tree t -> IO (Maybe Bits8)
  rightmost ptr = case !(out ptr) of
    "Leaf" # _ => pure Nothing
    "Node" # el => do
      (_ # br) <- poke el
      (b # r) <- poke br
      mval <- rightmost !(poke r)
      case mval of
        Just _ => pure mval
        Nothing => do
          b <- poke b
          pure (Just (getSingleton b))
\end{code}
%</pokerightmost>

\begin{code}
namespace Strict
  export
\end{code}
%<*viewrightmost>
\begin{code}
  rightmost : Pointer.Mu Tree t -> IO (Maybe Bits8)
  rightmost ptr = case !(view ptr) of
    "Leaf" # _ => pure Nothing
    "Node" # _ # b # r => do
      mval <- rightmost r
      case mval of
        Just _ => pure mval
        Nothing => pure (Just (getSingleton b))
\end{code}
%</viewrightmost>
\begin{code}

-- Here we define the functional specification of the functions we
-- want to write over pointers. This will, via Singleton & Serialising,
-- allow us to write correct by construction effectful functions
namespace Data

  ||| The size of the tree is given by the number of nodes
  public export
  size : Data.Mu Tree -> Nat
  size = fold $ \ k, v => case k of
    "Leaf" => 0
    "Node" => let (l # _ # r) = v in S (l + r)

  ||| The sum of the tree's node is given by casting the Bits8 nodes
  ||| to Nat and summing them up
  public export
  sum : Data.Mu Tree -> Nat
  sum = fold $ \ k, v => case k of
    "Leaf" => 0
    "Node" => let (l # b # r) = v in l + cast b + r

  ||| Map is obtained by applying a function transforming Bit8 values
  ||| to all of the Bits8 stored in the tree's nodes
  public export
  map : (Bits8 -> Bits8) -> Data.Mu Tree -> Data.Mu Tree
  map f = fold $ \ k, v => case k of
    "Leaf" => leaf
    "Node" => let (l # b # r) = v in node l (f b) r

-- Here we define the effectful functions processing a pointer
-- IO allows us to inspect the content addressed by the pointer to take the tree apart
-- Serialising lets us build a specified output tree
namespace Pointer

  ||| Correct by construction size function.
  ||| @ t   is the phantom name of the tree represented by the buffer
  ||| @ ptr is the position of t inside the buffer
  ||| Singleton ensures that the value we return is the same as if
  ||| we had directly processed `t`.
  export
  size : (ptr : Pointer.Mu Tree t) ->
         IO (Singleton (Data.size t))
  size ptr = case !(out ptr) of
    "Leaf" # t => pure (MkSingleton 0)
    "Node" # t => do
      l # br <- poke t
      m <- size !(poke l)
      _ # r <- poke br
      n <- size !(poke r)
      pure [| S [| m + n |] |]

  ||| Correct by construction sum function.
  ||| @ t   is the phantom name of the tree represented by the buffer
  ||| @ ptr is the position of t inside the buffer
  ||| Singleton ensures that the value we return is the same as if
  ||| we had directly processed `t`.
  export
\end{code}
%<*csum>
\begin{code}
  sum : Pointer.Mu Tree t ->
        IO (Singleton (Data.sum t))
  sum ptr = case !(view ptr) of
    "Leaf" # _ => pure [| Z |]
    "Node" # l # b # r =>
      do m <- sum l
         n <- sum r
         pure [| [| m + [| cast b |] |] + n |]
\end{code}
%</csum>
\begin{code}

  export
  leaf : Serialising Tree Tree.leaf
  leaf = setMuK Tree 0

  export
  node : Serialising Tree l -> Singleton b -> Serialising Tree r ->
            Serialising Tree (Tree.node l b r)
  node = setMuK Tree 1

  ||| Correct by construction map function.
  ||| @ f   is the function to map over the tree's nodes
  ||| @ t   is the phantom name of the tree represented by the buffer
  ||| @ ptr is the position of t inside the buffer
  ||| Serialising ensures that the value we compute is the same as if
  ||| we had directly processed `t`.
  export
\end{code}
%<*serialisedmap>
\begin{code}
  map : (f : Bits8 -> Bits8) ->
        (ptr : Pointer.Mu Tree t) ->
        Serialising Tree (Data.map f t)
  map f ptr = case !(view ptr) of
    "Leaf" # () => "Leaf" # ()
    "Node" # l # b # r => "Node" # map f l # [| f b |] # map f r
\end{code}
%</serialisedmap>
\begin{code}
  ||| Simple printing function
  export
  display : Pointer.Mu Tree t -> IO String
  display ptr = case !(out ptr) of
    "Leaf" # t => pure "Leaf"
    "Node" # t => do
      (l # b # r) <- layer t
      l <- display l
      r <- display r
      pure "(node \{l} \{show (getSingleton b)} \{r})"

namespace Serialising

  export
\end{code}
%<*execSerialising>
\begin{code}
  execSerialising : {cs : Data nm} -> {0 t : Data.Mu cs} ->
                    Serialising cs t -> IO (Pointer.Mu cs t)
\end{code}
%</execSerialising>
\begin{code}
  execSerialising act = do
    Just buf <- newBuffer blockSize
      | Nothing => failWith "\{__LOC__} Couldn't allocate buffer"
    middle <- setData buf 8 cs
    setInt buf 0 (middle - 8)
    end <- runSerialising act buf middle
    pure (MkMu buf middle (end - middle))


||| initFromFile creates a pointer to a datastructure stored in a file
||| @ cs   is the datatype you want to use to decode the buffer's content
||| @ safe signals whether to check the file's header matches the declared datatype
initFromFile : {default True safe : Bool} ->  (cs : Data nm) -> String -> IO (Exists (Pointer.Mu cs))
initFromFile cs fp
  = do Right buf <- createBufferFromFile fp
         | Left err => failWith (show err)
       skip <- getInt buf 0
       when safe $ do
         cs' <- getData buf 8
         unless (eqData cs cs') $ failWith $ unlines
           [ "Description mismatch:"
           , "expected:"
           , show cs
           , "but got:"
           , show cs'
           ]
       let pos = skip + 8
       pure (Evidence t (MkMu buf pos (!(rawSize buf) - pos)))

  where 0 t : Data.Mu cs -- postulated as an abstract value


namespace Data

  public export
  swap : Data.Mu Tree -> Data.Mu Tree
  swap ("Leaf" # _) = "Leaf" # ()
  swap ("Node" # l # b # r) = "Node" # r # b # l

namespace Pointer

  export
\end{code}
%<*swap>
\begin{code}
  swap : Pointer.Mu Tree t -> IO (Pointer.Mu Tree (Data.swap t))
  swap ptr = execSerialising $ case !(view ptr) of
    "Leaf" # () => leaf
    "Node" # l # b # r => node (copy r) b (copy l)
\end{code}
%</swap>
\begin{code}

testing : Pointer.Mu Tree t -> IO ()
testing tree = do
  putStrLn "Tree: \{!(display tree)}"
  putStrLn "RSum: \{show !(Raw.sum tree)}"
  putStrLn "Sum: \{show !(Pointer.sum tree)}"
  putStrLn "Rightmost: \{show !(rightmost tree)}"
  putStrLn "Tree size: \{show !(size tree)}"

main : IO ()
main = do
  -- First Tree
  writeToFile "tmp" example
  Evidence _ tree <- initFromFile Tree "tmp"
  testing tree

  putStrLn (replicate 72 '-')

  -- Second Tree: mapping over the first one
  serialiseToFile "tmp2" (map (1+) tree)
  Evidence _ tree2 <- initFromFile Tree "tmp2"
  testing tree2

  putStrLn (replicate 72 '-')

  -- Third Tree: don't go via a file
  tree3 <- execSerialising (map (2+) tree2)
  testing tree3
\end{code}
