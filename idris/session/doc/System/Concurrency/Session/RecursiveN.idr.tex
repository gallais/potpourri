\begin{code}
module System.Concurrency.Session.RecursiveN

import Control.Linear.LIO

import Data.DPair
import Data.List.HasLength
import Data.List.AtIndex
import Data.Nat

import Data.OpenUnion
import System
import System.File
import System.Concurrency as Threads
import System.Concurrency.Linear

import Language.Reflection

%default total


interface Logging io where
  logging : LinearIO io => String -> L io ()

[LOG] Logging io where
  logging = putStrLn
[QUIET] Logging io where
  logging str = pure ()

------------------------------------------------------------------------
-- Session types

namespace Session

  public export
  Kind : Type
  Kind = List String -- TODO: snoclists

  public export
  data Focus : (nm : String) -> (nms : Kind) -> Type where
    MkFocus :
      (thePrefix : SnocList String) ->
      (nm : String) ->
      (theSuffix : Kind) ->
      {auto theProof : nms === (thePrefix <>> nm :: theSuffix)} ->
      Focus nm nms

  export
  Uninhabited (Focus nm []) where
    uninhabited = go Refl where
      go : [] === xs -> Focus x xs -> Void
      go eq (MkFocus [<] _ suff @{Refl}) = uninhabited eq
      go eq foc@(MkFocus (pref :< x) nm suff @{Refl})
        = go eq (assert_smaller foc (MkFocus pref x (nm :: suff)))

  public export
  theSuffix : Focus nm nms -> Kind
  theSuffix (MkFocus _ _ suff) = suff

  public export
  data Norm = Head | Expr

  ||| A session type describes the interactions one thread may have with
  ||| another over a shared bidirectional channel: it may send or receive
  ||| values of arbitrary types, or be done communicating.
  public export
  data Session : Kind -> Norm -> Type where
    -- Recursive parts
    Fix : (nm : String) -> (s : Session (nm :: nms) Head) -> Session nms Head
    Rec : {nm : String} -> Focus nm nms -> Session nms Expr
    -- Usual operations
    Send : (ty : Type) -> (s : Session nms n) -> Session nms Head
    Recv : (ty : Type) -> (s : Session nms n) -> Session nms Head
    -- End a session
    End : Session nms Head
    -- Choice
    Offer : (s : Session nms m1) -> (t : Session nms m2) -> Session nms Head
    Select : (s : Session nms m1) -> (t : Session nms m2) -> Session nms Head

  ||| Dual describes how the other party to the communication sees the
  ||| interactions: our sends become their receives and vice-versa.
  public export
  Dual : Session nms m -> Session nms m
  Dual (Fix nm s) = Fix nm (Dual s)
  Dual (Rec pos) = (Rec pos)
  Dual (Send ty s) = Recv ty (Dual s)
  Dual (Recv ty s) = Send ty (Dual s)
  Dual End = End
  Dual (Offer s t) = Select (Dual s) (Dual t)
  Dual (Select s t) = Offer (Dual s) (Dual t)

  ||| Duality is involutive: the dual of my dual is me
  export
  dualInvolutive : (s : Session nms m) -> Dual (Dual s) === s
  dualInvolutive (Fix nm s) = cong (Fix nm) (dualInvolutive s)
  dualInvolutive (Rec pos) = Refl
  dualInvolutive (Send ty s) = cong (Send ty) (dualInvolutive s)
  dualInvolutive (Recv ty s) = cong (Recv ty) (dualInvolutive s)
  dualInvolutive End = Refl
  dualInvolutive (Offer s t) = cong2 Offer (dualInvolutive s) (dualInvolutive t)
  dualInvolutive (Select s t) = cong2 Select (dualInvolutive s) (dualInvolutive t)

||| We can collect the list of types that will be sent over the
||| course of a session by walking down its description
||| This definition is purely internal and will not show up in
||| the library's interface.
SendTypes : Session nms m -> List Type
SendTypes (Fix nm s) = SendTypes s
SendTypes (Rec pos) = []
SendTypes (Send ty s) = ty :: SendTypes s
SendTypes (Recv ty s) = SendTypes s
SendTypes End = []
SendTypes (Offer s1 s2) = SendTypes s1 ++ SendTypes s2
SendTypes (Select s1 s2) = SendTypes s1 ++ SendTypes s2

||| We can collect the list of types that will be received over
||| the course of a session by walking down its description
||| This definition is purely internal and will not show up in
||| the library's interface.
RecvTypes : Session nms m -> List Type
RecvTypes (Fix nm s) = RecvTypes s
RecvTypes (Rec pos) = []
RecvTypes (Send ty s) = RecvTypes s
RecvTypes (Recv ty s) = ty :: RecvTypes s
RecvTypes End = []
RecvTypes (Offer s1 s2) = RecvTypes s1 ++ RecvTypes s2
RecvTypes (Select s1 s2) = RecvTypes s1 ++ RecvTypes s2

||| The types received by my dual are exactly the ones I am sending
||| This definition is purely internal and will not show up in
||| the library's interface.
RecvDualTypes : (s : Session nms m) -> RecvTypes (Dual s) === SendTypes s
RecvDualTypes (Fix nm s) = RecvDualTypes s
RecvDualTypes (Rec pos) = Refl
RecvDualTypes (Send ty s) = cong (ty ::) (RecvDualTypes s)
RecvDualTypes (Recv ty s) = RecvDualTypes s
RecvDualTypes End = Refl
RecvDualTypes (Offer s1 s2) = cong2 (++) (RecvDualTypes s1) (RecvDualTypes s2)
RecvDualTypes (Select s1 s2) = cong2 (++) (RecvDualTypes s1) (RecvDualTypes s2)

||| The types sent by my dual are exactly the ones I receive
||| This definition is purely internal and will not show up in
||| the library's interface.
SendDualTypes : (s : Session nms m) -> SendTypes (Dual s) === RecvTypes s
SendDualTypes (Fix nm s) = SendDualTypes s
SendDualTypes (Rec pos)= Refl
SendDualTypes (Send ty s) = SendDualTypes s
SendDualTypes (Recv ty s) = cong (ty ::) (SendDualTypes s)
SendDualTypes End = Refl
SendDualTypes (Offer s1 s2) = cong2 (++) (SendDualTypes s1) (SendDualTypes s2)
SendDualTypes (Select s1 s2) = cong2 (++) (SendDualTypes s1) (SendDualTypes s2)

namespace Headed

  public export
  data Headed : Nat -> Nat -> Session m1 Head -> Session m2 k2 -> Type where
    Recv    : (ty : Type) -> {s : Session m2 k2} -> Headed 1 0 (Recv ty s) s
    Send    : (ty : Type) -> {s : Session m2 k2} -> Headed 0 1 (Send ty s) s
    OfferL  : {s1, s2 : _} -> Headed 0 0 (Offer s1 s2) s1
    OfferR  : {s1, s2 : _} -> Headed (length (RecvTypes s1)) (length (SendTypes s1)) (Offer s1 s2) s2
    SelectL : {s1, s2 : _} -> Headed 0 0 (Select s1 s2) s1
    SelectR : {s1, s2 : _} -> Headed (length (RecvTypes s1)) (length (SendTypes s1)) (Select s1 s2) s2

namespace Prefix

  public export
  data Prefix : Nat -> Nat -> Session m1 k1 -> Session m2 k2 -> Type where
    Lin  : Prefix 0 0 s s
    (:<) : Prefix p q s s' -> Headed m n s' s'' -> Prefix (p + m) (q + n) s s''

  public export infixl 6 :<!

  export
  (:<!) : Prefix p q s s' -> Headed m n s' s'' -> Prefix (m + p) (n + q) s s''
  pref :<! hd
    = rewrite plusCommutative m p in
      rewrite plusCommutative n q in
      pref :< hd

namespace Loop

  public export
  data Loop : Session nms Head -> Nat -> Nat -> Session nms k2 -> Type where
    Lin  : Loop o 0 0 o
    (:<) : Loop o p q s' -> Headed m n s' s'' -> Loop o (p + m) (q + n) s''

  export
  (:<!) : Loop o p q s' -> Headed m n s' s'' -> Loop o (m + p) (n + q) s''
  pref :<! hd
    = rewrite plusCommutative m p in
      rewrite plusCommutative n q in
      pref :< hd

-- TODO: snoc
namespace Env

  public export
  data Env : Kind -> Type where
    Nil  : Env []
    (::) : Session (nm :: nms) Head -> Env nms -> Env (nm :: nms)

  public export
  head : Env (e :: nms) -> Session (e :: nms) Head
  head (s :: _) = s

  public export
  tail : Env (e :: nms) -> Env nms
  tail (_ :: env) = env

  public export
  lookup : (f : Focus nm nms) -> Env nms -> Env (nm :: theSuffix f)
  lookup (MkFocus [<] nm suff @{Refl}) env = env
  lookup foc@(MkFocus (sx :< x) nm suff @{prf}) env
    = tail (lookup (assert_smaller foc (MkFocus sx x (nm :: suff) @{prf})) env)

  public export
  etaEnvCons : (env : Env (nm :: nms)) -> env === head env :: tail env
  etaEnvCons (_ :: _) = Refl


public export
data Context : Nat -> Nat -> Session m1 k1 -> Session m2 k2 -> Env m2 -> Type where
  InPrefix : {p, q : Nat} -> (0 pref : Prefix p q s s') ->
             Context p q s s' []
  Pumping  : {m, n, p, q : Nat} -> (ctx : Context p q s (Fix nm o) env) ->
             (0 loop : Loop o m n s') ->
             Context (p + m) (q + n) s s' (o :: env)


Sumer : Session [] Head
Sumer
  = Fix "serve"
  $ Offer End
  $ Send String
  $ Fix "getNs"
  $ Offer
      (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
      (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"]))

sumerCtx
  : Context 1 2 Sumer (Rec (MkFocus [<] "getNs" ["serve"]))
       [ Offer
          (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
          (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"]))
       , Offer End $ Send String $ Fix "getNs" $ Offer
          (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
          (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"]))
       ]
sumerCtx
  = Pumping (Pumping (InPrefix [<]) [<OfferR, Send String])
  $ [< OfferR, Recv Nat]

export
record Channel (s : Session nms k) (e : Env nms) where
  constructor MkChannel
  {sendStep    : Nat}
  {recvStep    : Nat}
  {0 ogNorm    : Norm}
  {0 ogKind    : Kind}
  {0 ogSession : Session ogKind ogNorm}
  context      : Context recvStep sendStep ogSession s e

  -- we throw in Bool for Offer & Select
  sendChan : Threads.Channel (Union (Bool :: SendTypes ogSession))
  recvChan : Threads.Channel (Union (Bool :: RecvTypes ogSession))

0 recvHeaded :
  Headed m n s s' ->
  AtIndex ty (RecvTypes s') i ->
  AtIndex ty (RecvTypes s) (m + i)
recvHeaded (Recv _) idx = S idx
recvHeaded (Send _) idx = idx
recvHeaded OfferL idx = weakenR idx
recvHeaded (OfferR {s1, s2}) idx
  = weakenL (Element _ (hasLength _)) idx
recvHeaded SelectL idx = weakenR idx
recvHeaded (SelectR {s1, s2}) idx
  = weakenL (Element _ (hasLength _)) idx

0 recvPrefix :
  Prefix m n start s ->
  AtIndex ty (RecvTypes s) i ->
  AtIndex ty (RecvTypes start) (m + i)
recvPrefix [<] idx = idx
recvPrefix ((:<) {m, p} pref s) idx
  = rewrite sym $ plusAssociative p m i in
    recvPrefix pref (recvHeaded s idx)

0 recvLoop :
  Loop o m n s ->
  AtIndex ty (RecvTypes s) i ->
  AtIndex ty (RecvTypes o) (m + i)
recvLoop [<] idx = idx
recvLoop ((:<) {p, m} loop s) idx
  = rewrite sym $ plusAssociative p m i in
    recvLoop loop (recvHeaded s idx)

0 recvContext :
  Context m n start s e ->
  AtIndex ty (RecvTypes s) i ->
  AtIndex ty (RecvTypes start) (m + i)
recvContext (InPrefix pref) idx = recvPrefix pref idx
recvContext (Pumping {p, m} pref loop) idx
  = rewrite sym $ plusAssociative p m i in
    recvContext pref (recvLoop loop idx)

Recv :
  (0 ty : Type) ->
  Context m n s (Recv ty s') e ->
  Context (S m) n s s' e
Recv ty (InPrefix pref)
  = InPrefix (pref :<! Recv ty)
Recv ty (Pumping {p, q, m, n} ctx loop)
  = rewrite plusSuccRightSucc p m in
    Pumping ctx (loop :<! Recv ty)

sendHeaded :
  Headed m n s s' ->
  AtIndex ty (SendTypes s') i ->
  AtIndex ty (SendTypes s) (n + i)
sendHeaded (Recv _) idx = idx
sendHeaded (Send _) idx = S idx
sendHeaded OfferL idx = weakenR idx
sendHeaded (OfferR {s1, s2}) idx
  = weakenL (Element _ (hasLength _)) idx
sendHeaded SelectL idx = weakenR idx
sendHeaded (SelectR {s1, s2}) idx
  = weakenL (Element _ (hasLength _)) idx


sendPrefix :
  Prefix m n start s ->
  AtIndex ty (SendTypes s) i ->
  AtIndex ty (SendTypes start) (n + i)
sendPrefix [<] idx = idx
sendPrefix ((:<) {n, q} pref s) idx
  = rewrite sym $ plusAssociative q n i in
    sendPrefix pref (sendHeaded s idx)

sendLoop :
  Loop o m n s ->
  AtIndex ty (SendTypes s) i ->
  AtIndex ty (SendTypes o) (n + i)
sendLoop [<] idx = idx
sendLoop ((:<) {q, n} loop s) idx
  = rewrite sym $ plusAssociative q n i in
    sendLoop loop (sendHeaded s idx)

0 sendContext :
  Context m n start s e ->
  AtIndex ty (SendTypes s) i ->
  AtIndex ty (SendTypes start) (n + i)
sendContext (InPrefix pref) idx = sendPrefix pref idx
sendContext (Pumping {q, n} ctx loop) idx
  = rewrite sym $ plusAssociative q n i in
    sendContext ctx (sendLoop loop idx)

Send :
  (0 ty : Type) ->
  Context m n s (Send ty s') e ->
  Context m (S n) s s' e
Send ty (InPrefix pref)
  = InPrefix (pref :<! Send ty)
Send ty (Pumping {p, q, m, n} ctx loop)
  = rewrite plusSuccRightSucc q n in
    Pumping ctx (loop :<! Send ty)

Unroll :
  {m, n : Nat} ->
  Context m n s (Fix nm o) e ->
  Context m n s o (o :: e)
Unroll ctx
  = rewrite plusCommutative 0 m in
    rewrite plusCommutative 0 n in
    Pumping ctx [<]

namespace Context

  export
  head :
    Context m n s _ env ->
    (m : Nat ** n : Nat ** Context m n s (head env) env)
  head (Pumping {p, q} ctx loop)
    = MkDPair p
    $ MkDPair q
    $ rewrite plusCommutative 0 p in
      rewrite plusCommutative 0 q in
      Pumping ctx [<]

  export
  tail :
    Context m n s s2 env ->
    (m : Nat ** n : Nat ** Context m n s (head (tail env)) (tail env))
  tail (Pumping ctx loop) = head ctx

ContinueAux :
  (pos : Focus nm nms) ->
  Context m n s s2 env ->
  (m : Nat ** n : Nat ** Context m n s (head (lookup pos env)) (lookup pos env))
ContinueAux pos (InPrefix pref) = absurd pos
ContinueAux (MkFocus [<] nm theSuffix @{Refl}) ctx@(Pumping _ _)
  = head ctx
ContinueAux foc@(MkFocus (sx :< x) nm suff @{prf}) ctx@(Pumping _ _)
  = let (_ ** _ ** ctx) = ContinueAux (assert_smaller foc (MkFocus sx x (nm :: suff) @{prf})) ctx in
    tail ctx

Continue :
  (pos : Focus nm nms) ->
  Context m n s (Rec pos) env ->
  (m : Nat ** n : Nat ** Context m n s (head (lookup pos env)) (lookup pos env))
Continue pos = ContinueAux {s2 = Rec pos} pos


Continue'Aux :
  Context m n s o (o :: env) ->
  (m : Nat ** n : Nat ** Context m n s (Fix nm o) env)
Continue'Aux (Pumping ctx loop) = (_ ** _ ** ctx)

Continue' :
  (pos : Focus nm nms) ->
  Context m n s (Rec pos) env ->
  (m : Nat ** n : Nat ** Context m n s (Fix nm (head (lookup pos env))) (tail (lookup pos env)))
Continue' pos ctx = case Continue pos ctx of
  (p ** q ** ctx) => Continue'Aux (rewrite sym (etaEnvCons (lookup pos env)) in ctx)


OfferL :
  Context m n s (Offer s1 s2) e ->
  Context m n s s1 e
OfferL (InPrefix pref) = InPrefix (pref :<! OfferL)
OfferL (Pumping ctx loop) = Pumping ctx (loop :<! OfferL)

OfferR :
  {s1 : _} ->
  Context m n s (Offer s1 s2) e ->
  (m : Nat ** n : Nat ** Context m n s s2 e)
OfferR (InPrefix pref) = (_ ** _ ** InPrefix (pref :<! OfferR))
OfferR (Pumping pref loop) = (_ ** _ ** Pumping pref (loop :<! OfferR))

SelectL :
  Context m n s (Select s1 s2) e ->
  Context m n s s1 e
SelectL (InPrefix pref) = InPrefix (pref :<! SelectL)
SelectL (Pumping pref loop) = Pumping pref (loop :<! SelectL)

SelectR :
  {s1 : _} ->
  Context m n s (Select s1 s2) e ->
  (m : Nat ** n : Nat ** Context m n s s2 e)
SelectR (InPrefix pref) = (_ ** _ ** InPrefix (pref :<! SelectR))
SelectR (Pumping pref loop) = (_ ** _ ** Pumping pref (loop :<! SelectR))

parameters {0 io : Type -> Type} {auto _ : LinearIO io}

  public export
  0 IO1 : Type -> Type
  IO1 = L1 io

  parameters {0 e : Env k} {s1, s2 : Session k _}

    export
\end{code}
%<*offerselect>
\begin{code}
    offer : Channel (Offer s1 s2) e -@
      IO1 (Res Bool (\ b => ifThenElse b (Channel s1 e) (Channel s2 e)))
    select : Channel (Select s1 s2) e -@
      ((b : Bool) -> IO1 (ifThenElse b (Channel s1 e) (Channel s2 e)))
\end{code}
%</offerselect>
\begin{code}
    offer (MkChannel ctxt sendCh recvCh) = do
      x@(Element idx prf val) <- channelGet recvCh
      -- Here we check that we got the right message by projecting out of
      -- the union type using the current `recvStep`. Both ends should be
      -- in sync because of the `RecvDualTypes` and `SendDualTypes` lemmas.
      let Just val = prj 0 Z x
        | Nothing => do
          die1 "Error: invalid recv expected (Offer selection) but got \{show idx}"
      pure1 $ val # if val
        then MkChannel (OfferL ctxt) sendCh recvCh
        else let (_ ** _ ** ctxt) = OfferR ctxt in MkChannel ctxt sendCh recvCh

    select (MkChannel ctxt sendCh recvCh) b = do
      let val = inj 0 Z b
      channelPut sendCh val
      -- Here we check that we got the right message by projecting out of
      -- the union type using the current `recvStep`. Both ends should be
      -- in sync because of the `RecvDualTypes` and `SendDualTypes` lemmas.
      pure1 $ if b
        then MkChannel (SelectL ctxt) sendCh recvCh
        else let (_ ** _ ** ctxt) := SelectR ctxt in MkChannel ctxt sendCh recvCh


  export
\end{code}
%<*close>
\begin{code}
  close : Channel End e -@ L io ()
\end{code}
%</close>
\begin{code}
  close (MkChannel{}) = pure ()

  export
\end{code}
%<*sendrecv>
\begin{code}
  recv : Channel (Recv ty s) e -@ IO1 (Res ty (\ _ => Channel s e))
  send : Channel (Send ty s) e -@ (ty -> IO1 (Channel s e))
\end{code}
%</sendrecv>
\begin{code}
  recv (MkChannel {recvStep} ctxt sendCh recvCh) = do
    x@(Element k prf val) <- channelGet recvCh
    -- Here we check that we got the right message by projecting out of
    -- the union type using the current `recvStep`. Both ends should be
    -- in sync because of the `RecvDualTypes` and `SendDualTypes` lemmas.
    let Just val = prj (S recvStep) (rewrite plusCommutative 0 recvStep in S (recvContext ctxt Z)) x
      | Nothing => die1 "Error: invalid recv expected \{show recvStep} but got \{show k}"
    pure1 (val # MkChannel (Recv ty ctxt) sendCh recvCh)

  send (MkChannel {sendStep} ctxt sendCh recvCh) v = do
    let val = inj (S sendStep) (rewrite plusCommutative 0 sendStep in S (sendContext ctxt Z)) v
    channelPut sendCh val
    -- Here we check that we got the right message by projecting out of
    -- the union type using the current `recvStep`. Both ends should be
    -- in sync because of the `RecvDualTypes` and `SendDualTypes` lemmas.
    pure1 (MkChannel (Send ty ctxt) sendCh recvCh)

  public export
  0 TypeAt : (pos : Focus nm nms) -> Env nms -> Session (nm :: theSuffix pos) Head
  TypeAt pos e = head (lookup pos e)

  public export
  0 EnvAt : (pos : Focus nm nms) -> Env nms -> Env (nm :: theSuffix pos)
  EnvAt pos e = lookup pos e

  export
\end{code}
%<*unrollcontinue>
\begin{code}
  unroll : Channel (Fix nm s) e -@ IO1 (Channel s (s :: e))
  continue : {pos : _} -> Channel (Rec pos) e -@ IO1 (Channel (TypeAt pos e) (EnvAt pos e))
  continue'   : {pos : _} -> Channel (Rec pos) e -@ IO1 (Channel (Fix nm (TypeAt pos e)) (tail $ EnvAt pos e))
  roll   : {pos : _} -> Channel s (s :: e) -@ IO1 (Channel (Fix nm s) e)
\end{code}
%</unrollcontinue>
\begin{code}
  unroll (MkChannel ctxt sendCh recvCh) = do
    pure1 (MkChannel (Unroll ctxt) sendCh recvCh)

  continue (MkChannel ctxt sendCh recvCh) = do
    let (p ** q ** ctxt) = Continue pos ctxt
    pure1 (MkChannel ctxt sendCh recvCh)

  continue' (MkChannel ctxt sendCh recvCh) = do
    let (p ** q ** ctxt) = Continue' pos ctxt
    pure1 (MkChannel ctxt sendCh recvCh)

  roll (MkChannel ctxt sendCh recvCh) = do
    let (p ** q ** ctxt) = Continue'Aux ctxt
    pure1 (MkChannel ctxt sendCh recvCh)


||| Given a session, create a bidirectional communiaction channel and
||| return its two endpoints
export
makeChannel :
  LinearIO io =>
  (0 s : Session [] m) ->
  L1 io (LPair (Channel s []) (Channel (Dual s) []))
makeChannel s = do
  sendChan <- Threads.makeChannel
  recvChan <- Threads.makeChannel
  let 1 posCh : Channel s []
    := MkChannel (InPrefix [<]) sendChan recvChan
  let 1 negCh : Channel (Dual s) []
    := MkChannel (InPrefix [<])
         (rewrite SendDualTypes s in recvChan)
         (rewrite RecvDualTypes s in sendChan)
  pure1 (posCh # negCh)


||| Given a session and two functions communicating according to that
||| session, we can run the two programs concurrently and collect their
||| final results.
||| The `src` and `tgt` names are purely for logging purposes.
export
fork : (0 s : Session [] m) ->
       (Channel s        [] -@ L IO a) -@
       (Channel (Dual s) [] -@ L IO b) -@
       L IO (a, b)
fork s kA kB = do
  (posCh # negCh) <- RecursiveN.makeChannel s
  -- Forked threads cannot return anything so we grab a couple of
  -- low-level channels just to get the results back out
  par (kA posCh) (kB negCh)

------------------------------------------------------------------------
-- Example
------------------------------------------------------------------------


client : LinearIO io =>
  List (List Nat) -> -- list of arguments
  SnocList Nat -> -- accumulator of results
  Channel (Dual Sumer) [] -@ L io (List Nat)

sender : LinearIO io =>
  List Nat -> -- list of arguments
  List (List Nat) -> -- list of list arguments
  SnocList Nat -> -- accumulator of results
  Channel (Dual $ Offer
        (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
        (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"])))
    [ Dual $ Offer
        (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
        (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"]))
    , Dual $ Offer End $ Send String $ Fix "getNs" $ Offer
        (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
        (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"]))
    ]
 -@ L io (List Nat)

client [] acc ch = do
  ch <- unroll ch
  ch <- select ch True
  close ch
  pure (acc <>> [])
client (ns :: nss) acc ch = do
  ch <- unroll ch
  ch <- select ch False
  (msg # ch) <- recv ch
  putStrLn msg
  ch <- unroll ch
  sender ns nss acc ch

sender [] nss acc ch = do
  ch <- select ch True
  (n # ch) <- recv ch
  ch <- continue' ch
  client nss (acc :< n) ch
sender (n :: ns) nss acc ch = do
  ch <- select ch False
  ch <- send ch n
  ch <- continue ch
  sender ns nss acc ch

covering
server : LinearIO io =>
  Nat -> -- ID
  Channel Sumer [] -@ L io ()

covering
adder : LinearIO io =>
  Nat -> -- accumulator
  Channel (Offer
        (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
        (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"])))
    [ Offer
        (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
        (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"]))
    , Offer End $ Send String $ Fix "getNs" $ Offer
        (Send Nat $ Rec (MkFocus [<"getNs"] "serve" []))
        (Recv Nat $ Rec (MkFocus [<] "getNs" ["serve"]))
    ] -@
    L1 io (Channel Sumer [])


\end{code}
%<*server>
\begin{code}
server id ch = do
  ch <- unroll ch
  b # ch <- offer ch
  if b then close ch else do
    let msg = "Welcome nÂ°\{show id}!"
    ch <- send ch msg
    ch <- unroll ch
    ch <- adder 0 ch
    server (1 + id) ch
\end{code}
%</server>
\begin{code}


\end{code}
%<*adder>
\begin{code}
adder acc ch = do
  b # ch <- offer ch
  if b
    then do
      ch <- send ch acc
      ch <- continue' ch
      pure1 ch
    else do
      (n # ch) <- recv ch
      ch <- continue ch
      adder (acc + n) ch
\end{code}
%</adder>
\begin{code}

covering
main : IO ()
main = do
  let %hint loggingIO : Logging IO; loggingIO = QUIET
  (_, sums) <- LIO.run $ fork Sumer (server 0) (client [[1..5], [5..10]] [<])
  putStrLn "Results are \{show sums}"
\end{code}
