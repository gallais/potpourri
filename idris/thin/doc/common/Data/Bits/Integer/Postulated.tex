\begin{code}
||| Postulated properties of Data.Bits for Integer
||| NB: some of these are probably provable

module Data.Bits.Integer.Postulated

import Data.Bits
import Decidable.Equality

%default total

||| This should be far safer than `believe_me` as:
||| 1. we only reduce to `Refl` when x actually equals y
||| 2. we don't expose the implementation so that users can't force reductions in an absurd context
unsafeRefl : DecEq a => {x, y : a} -> x === y
unsafeRefl {x} {y} with (decEq x y)
  _ | Yes eq = eq
  _ | No neq = assert_total $ idris_crash "Equality postulated using unsafeRefl is incorrect"

------------------------------------------------------------------------------
-- Extensional equality
------------------------------------------------------------------------------

infix 6 ~~~
public export
\end{code}
%<*extensionalEq>
\begin{code}
(~~~) : Integer -> Integer -> Type
bs ~~~ cs = (i : Nat) -> testBit bs i === testBit cs i
\end{code}
%</extensionalEq>
\begin{code}

export
\end{code}
%<*extensionally>
\begin{code}
extensionally : {bs, cs : Integer} -> bs ~~~ cs -> bs === cs
\end{code}
%</extensionally>
\begin{code}
extensionally fext = unsafeRefl


------------------------------------------------------------------------------
-- Postulated properties
------------------------------------------------------------------------------

export
\end{code}
%<*testBitShiftR>
\begin{code}
testBitShiftR : (bs : Integer) -> (k : Nat) ->
  (i : Nat) -> testBit (bs `shiftR` k) i === testBit bs (k + i)
\end{code}
%</testBitShiftR>
\begin{code}
testBitShiftR bs k i = unsafeRefl

export
\end{code}
%<*testBit0ShiftL>
\begin{code}
testBit0ShiftL : (bs : Integer) -> (k : Nat) ->
  testBit (bs `shiftL` S k) Z === False
\end{code}
%</testBit0ShiftL>
\begin{code}
testBit0ShiftL bs k = unsafeRefl

export

\end{code}
%<*testBitSShiftL>
\begin{code}
testBitSShiftL : (bs : Integer) -> (k : Nat) -> (i : Nat) ->
  testBit (bs `shiftL` S k) (S i) === testBit (bs `shiftL` k) i
\end{code}
%</testBitSShiftL>
\begin{code}
testBitSShiftL bs k i = unsafeRefl

export
\end{code}
%<*shiftL0>
\begin{code}
shiftL0 : (bs : Integer) -> (bs `shiftL` 0) === bs
\end{code}
%</shiftL0>
\begin{code}
shiftL0 bs = unsafeRefl

export
||| Postulated: conjunction is bitwise on integers
\end{code}
%<*testBitAnd>
\begin{code}
testBitAnd : (bs, cs : Integer) -> (i : Nat) ->
  testBit (bs .&. cs) i === (testBit bs i && testBit cs i)
\end{code}
%</testBitAnd>
\begin{code}
testBitAnd bs cs i = unsafeRefl

export
||| Postulated: disjunction is bitwise on integers
\end{code}
%<*testBitOr>
\begin{code}
testBitOr : (bs, cs : Integer) -> (i : Nat) ->
  testBit (bs .|. cs) i === (testBit bs i || testBit cs i)
\end{code}
%</testBitOr>
\begin{code}
testBitOr bs cs i = unsafeRefl

export
\end{code}
%<*testSetBitOther>
\begin{code}
testSetBitOther : (bs : Integer) -> (i, j : Nat) -> Not (i === j) ->
  testBit (setBit bs i) j === testBit bs j
\end{code}
%</testSetBitOther>
\begin{code}
testSetBitOther bs i j neq = unsafeRefl

export
||| Postulated: complement is bitwise on integers
\end{code}
%<*testBitComplement>
\begin{code}
testBitComplement : (bs : Integer) -> (i : Nat) ->
  testBit (complement bs) i === not (testBit bs i)
\end{code}
%</testBitComplement>
\begin{code}
testBitComplement bs i = unsafeRefl

export
\end{code}
%<*bitNonZero>
\begin{code}
bitNonZero : (i : Nat) -> (bit i == 0) === False
\end{code}
%</bitNonZero>
\begin{code}
bitNonZero i = unsafeRefl
\end{code}
