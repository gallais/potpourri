\begin{code}
module VectAsList

%default total

export
\end{code}
%<*vect>
\begin{code}
record Vect (n : Nat) (a : Type) where
  constructor MkVect
  encoding : List a
  0 valid : length encoding === n
\end{code}
%</vect>
\begin{code}

namespace Smart

  public export
\end{code}
%<*nil>
\begin{code}
  Nil : Vect Z a
  Nil = MkVect [] Refl
\end{code}
%</nil>

\begin{code}
  public export
\end{code}
%<*cons>
\begin{code}
  (::) : a -> Vect n a -> Vect (S n) a
  x :: MkVect xs eq = MkVect (x :: xs) (cong S eq)
\end{code}
%</cons>

\begin{code}
public export
\end{code}
%<*dataview>
\begin{code}
data View : Vect n a -> Type where
  Nil : View Nil
  (::) : (x : a) -> (xs : Vect n a) -> View (x :: xs)
\end{code}
%</dataview>
\begin{code}

namespace Smart

  public export
\end{code}
%<*view>
\begin{code}
  view : (xs : Vect n a) -> View xs
  view (MkVect [] Refl) = Nil
  view (MkVect (x :: xs) Refl) = x :: MkVect xs Refl
\end{code}
%</view>
\begin{code}

export
\end{code}
%<*map>
\begin{code}
map : (a -> b) -> Vect n a -> Vect n b
map f xs@_ with (view xs)
  _ | [] = []
  _ | hd :: tl = f hd :: map f tl
\end{code}
%</map>


%<*append>
\begin{code}
(++) : Vect m a -> Vect n a -> Vect (m + n) a
xs@_ ++ ys with (view xs)
  _ | [] = ys
  _ | hd :: tl = hd :: (tl ++ ys)
\end{code}
%</append>
\begin{code}

\end{code}
%<*splitAtrel>
\begin{code}
data SplitAt : (m : Nat) -> Vect p a -> Type where
  MkSplitAt : (xs : Vect m a) -> (ys : Vect n a) ->
              SplitAt m (xs ++ ys)
\end{code}
%</splitAtrel>
\begin{code}

namespace SplitAt


 export
\end{code}
%<*splitAtcons>
\begin{code}
 (::) : (x : a) -> SplitAt m xs -> SplitAt (S m) (x :: xs)
 x :: MkSplitAt pref@(MkVect _ Refl) suff
   = MkSplitAt (x :: pref) suff
\end{code}
%</splitAtcons>
\begin{code}

\end{code}
%<*splitAt>
\begin{code}
splitAt : (m : Nat) -> (xs : Vect (m + n) a) -> SplitAt m xs
splitAt 0 xs = MkSplitAt [] xs
splitAt (S m) xs@_ with (view xs)
  _ | hd :: tl = hd :: splitAt m tl
\end{code}
%</splitAt>
\begin{code}
